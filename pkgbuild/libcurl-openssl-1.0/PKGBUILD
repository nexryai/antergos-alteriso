# Maintainer: Michael Straube <straubem@gmx.de>
# Contributor: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Piotr Balcerowski <piotr@balcerowski.org>
# Contributor: Florian Maunier <fmaunier@gmail.com>

pkgname=libcurl-openssl-1.0
pkgver=7.54.1
pkgrel=1
pkgdesc='An URL retrieval library (build against openssl-1.0)'
arch=('x86_64')
url='https://curl.haxx.se'
license=('MIT')
depends=('curl' 'glibc' 'krb5' 'openssl-1.0' 'libpsl' 'zlib' 'libssh2.so')
options=('strip')
source=("https://curl.haxx.se/download/curl-${pkgver}.tar.gz")
sha512sums=('59c236f46e13faf9124cecab991b139e57376b1b5a5db13005f55ffece67191f0f7ecb954ae14e382a561d34f8b652b532755746d0cda79713b56857833951c5')

build() {
  cd curl-${pkgver}

  export PKG_CONFIG_PATH=/usr/lib/openssl-1.0/pkgconfig

  ./configure \
    --prefix='/usr' \
    --disable-ldap \
    --disable-ldaps \
    --disable-manual \
    --enable-ipv6 \
    --enable-threaded-resolver \
    --with-gssapi \
    --with-random='/dev/urandom' \
    --with-ca-bundle='/etc/ssl/certs/ca-certificates.crt'

  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make -C lib
}

package() {
  cd curl-${pkgver}

  make -C lib DESTDIR="${pkgdir}" install

  mv "${pkgdir}"/usr/lib/libcurl{,-openssl-1.0}.so.4.4.0
  rm "${pkgdir}"/usr/lib/libcurl.{a,so}*
  ln -s libcurl-openssl-1.0.so.4.4.0 "${pkgdir}"/usr/lib/libcurl-openssl-1.0.so

  install -d "${pkgdir}"/usr/lib/libcurl-openssl-1.0
  for _i in so so.4 so.4.4.0; do
    ln -s ../libcurl-openssl-1.0.so.4.4.0 "${pkgdir}"/usr/lib/libcurl-openssl-1.0/libcurl.${_i}
  done

  install -dm 755 "${pkgdir}"/usr/share/licenses
  ln -s curl "${pkgdir}"/usr/share/licenses/${pkgname}
}
