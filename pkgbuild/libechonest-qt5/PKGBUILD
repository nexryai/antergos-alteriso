# Maintainer: Antergos Developers <dev@antergos.com>
# Contributor: Evgeniy Alekseev <arcanis at archlinux dot org>
# Contributor: Kuba Serafinowski <zizzfizzix at gmail dot com>
# Contributor: Rick W. Chen <stuffcorpse at archlinux dot us>

pkgname=libechonest-qt5
_pkgname=libechonest
pkgver=2.3.1
pkgrel=3
pkgdesc="C++ library for interfacing with Echo Nest"
arch=('x86_64')
url='https://projects.kde.org/projects/playground/libs/libechonest'
license=('GPL')
depends=('qt5-base')
makedepends=('cmake')
source=("http://files.lfranchi.com/${_pkgname}-${pkgver}.tar.bz2")
md5sums=('d8c60545b056145dc66882971a0acf9c')

prepare() {
  rm -rf "build"
  mkdir "build"
}

build() {
  cd "build"
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=Release \
        -DECHONEST_BUILD_TESTS=OFF \
        -DBUILD_WITH_QT4=OFF \
        "../${_pkgname}-${pkgver}"
  make
}

package() {
  cd "build"
  make DESTDIR="${pkgdir}" install
}
