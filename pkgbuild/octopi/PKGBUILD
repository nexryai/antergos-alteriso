# Maintainer: Antergos devs <devs@antergos.com>
# AUR Maintainer: kikadf <kikadf.01@gmail.com>
# AUR Maintainer: Jameson Pugh <imntreal@gmail.com>

pkgname=octopi
pkgver=0.9.0
pkgrel=1
pkgdesc="This is Octopi, a powerful Pacman frontend using Qt libs"
url="http://octopiproject.wordpress.com"
arch=('x86_64')
license=('GPL2')
depends=('qtermwidget' 'pkgfile' 'alpm_octopi_utils' 'qt5-declarative' 'pacman-contrib')
optdepends=('pikaur: for AUR support'
            'yaourt: for AUR support'
            'pacaur: for AUR support'
            'trizen: for AUR support'
            'pacmanlogviewer: to view pacman log files'
            'gksu: for XFCE, Gnome, LXDE, Cinnamon'
            'lxqt-sudo: for LXQT')
provides=('octopi-kde-git')
conflicts=('octopi-git' 'octopi-kde-git')
source=('octopi-repoeditor.desktop'
        "https://code.chakralinux.org/packages/desktop/raw/master/octopi/0001-remove-qtermwidget.patch"
        "https://gitlab.com/kikadf/patches/raw/master/octopi/0002-add-pikaur-support.patch"
        "icons::git+https://gitlab.com/kikadf/octopicons.git"
        "https://github.com/aarnt/${pkgname}/archive/v${pkgver}.tar.gz")
sha256sums=('131f16745df685430db55e54ede6da66aed9b02ca00d6d873a002b2a3e1c90ef'
            'bdf668ae3bcbac4bf908d96ce4115bac77bf8d4e58796d12fc2cd4e8ee8294e7'
            '18d3ee70f520045035c5d9ede6dc5e6121f0f258fe4eaa743551aaf6bd9b0181'
            'SKIP'
            'f6c7fe6bbcc26a79b1e455a42a8dc906bee4edd1b386b4f76178f006f4d7f822')

prepare() {
    cd ${pkgname}-${pkgver}

    # add pikaur support
    patch -Np1 -i ../0002-add-pikaur-support.patch

    cp resources/images/octopi_green.png resources/images/octopi.png
}

build() {
    cd ${pkgname}-${pkgver}

    msg "Building octopi..."
    qmake-qt5 octopi.pro
    make

    cd notifier/pacmanhelper
    msg "Building pacmanhelper..."
    qmake-qt5 pacmanhelper.pro
    make

    cd ../octopi-notifier
    msg "Building octopi-notifier..."
    qmake-qt5 octopi-notifier.pro
    make

    cd ../../repoeditor
    msg "Building octopi-repoeditor..."
    qmake-qt5 octopi-repoeditor.pro
    make

    cd ../cachecleaner
    msg "Building octopi-cachecleaner..."
    qmake-qt5 octopi-cachecleaner.pro
    make
}

package() {
    cd ${pkgname}-${pkgver}
    make INSTALL_ROOT=${pkgdir} install

    cd notifier/pacmanhelper
    make INSTALL_ROOT=${pkgdir} install
    cd ../..

    cd notifier/octopi-notifier
    make INSTALL_ROOT=${pkgdir} install
    cd ../..

    cd repoeditor
    make INSTALL_ROOT=${pkgdir} install
    install -D -m644 "${srcdir}/octopi-repoeditor.desktop" "${pkgdir}/usr/share/applications/octopi-repoeditor.desktop"
    cd ..

    cd cachecleaner
    make INSTALL_ROOT=${pkgdir} install
    cd ..

    #speedup files
    install -D -m755 "speedup/speedup-octopi.sh" "${pkgdir}/usr/bin/speedup-octopi.sh"
    install -D -m644 "speedup/octopi.service" "${pkgdir}/usr/lib/systemd/system/octopi.service"

    # Add some icons to customize notifier
    mkdir -p "${pkgdir}/usr/share/octopi/icons/"
    cp ../icons/*.png "${pkgdir}/usr/share/octopi/icons/"
}
