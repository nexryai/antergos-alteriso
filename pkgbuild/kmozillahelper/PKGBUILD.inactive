# Maintainer: Antergos Developers <dev@antergos.com>

pkgname=kmozillahelper
_pkgver=5.0.1
pkgver=${_pkgver}
pkgrel=2
pkgdesc="Mozilla KDE integration by openSUSE."
url="https://github.com/openSUSE/kmozillahelper"
arch=("i686" "x86_64")
license=('MIT')
depends=("kdoctools" "ki18n" "knotifications" "kio")
makedepends=("cmake" "automoc4" "extra-cmake-modules" "git")
provides=("kmozillahelper-frameworks" "kmozillahelper-kf5")
conflicts=("kmozillahelper-frameworks" "kmozillahelper-kf5")
replaces=("kmozillahelper-frameworks" "kmozillahelper-kf5")
source=("${pkgname}-${pkgver}::https://github.com/openSUSE/kmozillahelper/archive/v${pkgver}.tar.gz")
md5sums=('58816fb988dc01a1b141a1d70ecd70ef')


build() {
	mkdir -p "${srcdir}/${pkgname}-build"
	cd "${srcdir}/${pkgname}-build"

	cmake -DCMAKE_INSTALL_PREFIX=/usr \
		-DBUILD_TESTING=OFF \
		-DCMAKE_BUILD_TYPE=Release \
		"${srcdir}/${pkgname}-${pkgver}"
	make
}

package() {
	cd "${srcdir}/${pkgname}-build"
	make DESTDIR="${pkgdir}" install
}
