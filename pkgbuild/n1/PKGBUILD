# Maintainer: Antergos Developers <dev@antergos.com>
# Contributor: D. Can Celasun <dcelasun[at]gmail[dot]com>

pkgname=n1
pkgver=1.5.0
buildver=b0ec335
pkgrel=3
pkgdesc="A mail client for Mac, Linux, and Windows that is built on the modern web and designed to be extended."
arch=('x86_64')
url="https://nylas.com/N1/"
license=('GPLv3')
depends=('gtk2' 'libgnome-keyring' 'desktop-file-utils' 'python2' 'gconf' 'nodejs'
		'libnotify' 'libxtst' 'nss' 'alsa-lib' 'xdg-utils')
optdepends=('gnome-keyring: provides secret service')
groups=('email') 
source=("https://github.com/nylas/N1/releases/download/${pkgver}/N1-${pkgver}.deb")
install=${pkgname}.install
md5sums=('0510570bc107f4329c99b62719d774c4')


# BEGIN ANTBS METADATA
_is_monitored='True'
_monitored_service='github'
_monitored_type='releases'
_monitored_project='nylas'
_monitored_repo='N1'
_monitored_match_pattern='1.5.0'
# END ANTBS METADATA


package() {
	msg2 "Extracting data.tar.gz..."
	bsdtar -xf data.tar.gz -C "${pkgdir}/"

	cd "${pkgdir}"
	find -name '*.py' -exec sed -i 's|^#!/usr/bin/env python\s*$|#!/usr/bin/env python2|' {} \;
	find -name '*.py' -exec sed -i 's|^#!/usr/bin/python\s*$|#!/usr/bin/python2|' {} \;

	mkdir -p "${pkgdir}"/usr/share/licenses/nylas/

	rm -rf "${pkgdir}/usr/bin/nylas"
	ln -s /usr/share/nylas/nylas "${pkgdir}"/usr/bin/nylas
	ln -s /usr/share/nylas/LICENSE "${pkgdir}"/usr/share/licenses/nylas/LICENSE
}
