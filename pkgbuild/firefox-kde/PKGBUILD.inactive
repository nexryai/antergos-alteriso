# Maintainer: Antergos Developers <dev@antergos.com>
# Contributor: Weng Xuetian <wengxt@gmail.com>
# Contributor: Thaodan <theodorstormgrade@gmail.com>

_pkgname=firefox
pkgname=firefox-kde
pkgver=54.0.1
pkgrel=1
pkgdesc="Standalone web browser from mozilla.org with openSUSE KDE integration patches."
arch=('x86_64')
license=('MPL' 'GPL' 'LGPL')
url='https://build.opensuse.org/package/show/mozilla:Factory/MozillaFirefox'
groups=('browser')

depends=('alsa-lib'        'dbus-glib'  'ffmpeg'  'gtk2'
        'gtk3'            'hunspell'   'icu'     'kmozillahelper'
        'libevent'        'libvpx'     'libxt'   'mime-types'
        'mozilla-common'  'nss'        'sqlite'  'startup-notification'
        'ttf-font'        'libpulse')

makedepends=('autoconf2.13'  'cargo'      'diffutils'        'gconf'
			'imake'         'inetutils'  'mesa'
			'python2'       'unzip'      'xorg-server-xvfb' 'yasm'
			'zip')

optdepends=('networkmanager: Location detection via available WiFi networks'
			'upower: Battery API'
			'speech-dispatcher: Text-to-Speech'
			'pulseaudio: Audio support')

provides=("firefox=${pkgver}")
conflicts=('firefox')
options=('!emptydirs' '!makeflags' '!strip')
_patchrev=53443ffb496a
_patchurl="http://www.rosenauer.org/hg/mozilla/raw-file/${_patchrev}"
_archpkg_git_url='https://git.archlinux.org/svntogit/packages.git/plain/trunk'

validpgpkeys=(
	'14F26682D0916CDD81E37B6D61B7B526D98F0353'
	'F2EF4E6E6AE75B95F11F1EB51C69C4E55E9905DB'
) # Mozilla Software Releases <release@mozilla.com>
# gpg --keyserver pool.sks-keyservers.net --recv-keys "${validpgpkeys[0]}"

_srcurl="https://ftp.mozilla.org/pub/firefox/releases/${pkgver}"
_srcfile="source/${_pkgname}-${pkgver}.source.tar.xz"
source=("${_srcurl}/${_srcfile}"
		"${_srcurl}/SHA256SUMS"{,.asc}
		"fix-wifi-scanner.diff::${_archpkg_git_url}/fix-wifi-scanner.diff?h=packages/firefox"
		"firefox-install-dir.patch::${_archpkg_git_url}/firefox-install-dir.patch?h=packages/firefox"
		"no-crmf.diff::${_archpkg_git_url}/no-crmf.diff?h=packages/firefox"
		'vendor.js'
		'mozconfig'
		'firefox.desktop'
		'kde.js'
		# Firefox patchset
		$_patchurl/firefox-branded-icons.patch
		$_patchurl/firefox-kde.patch
		$_patchurl/firefox-no-default-ualocale.patch
		# Gecko/toolkit patchset
		$_patchurl/mozilla-kde.patch
		$_patchurl/mozilla-language.patch
		$_patchurl/mozilla-nongnome-proxies.patch
		$_patchurl/mozilla-reduce-files-per-UnifiedBindings.patch)
sha256sums=(
	$(wget -q "${source[1]}" && grep "${_srcfile}" SHA256SUMS | cut -f1 -d ' ')
	'SKIP'
	'SKIP'
	'9765bca5d63fb5525bbd0520b7ab1d27cabaed697e2fc7791400abc3fa4f13b8'
	'd86e41d87363656ee62e12543e2f5181aadcff448e406ef3218e91865ae775cd'
	'fb85a538044c15471c12cf561d6aa74570f8de7b054a7063ef88ee1bdfc1ccbb'
	'93c5df00f409988bbfa890ac175103476ead3af68f7501973ee70bd11dc472f8'
	'09123d4a390045eba9905e88295c2f4663eb625839e2541c3207f76f4e3c2128'
	'c202e5e18da1eeddd2e1d81cb3436813f11e44585ca7357c4c5f1bddd4bec826'
	'0850a8a8dea9003c67a8ee1fa5eb19a6599eaad9f2ad09db753b74dc5048fdbc'
	'c85a37e71f2bd511fac88d41ca0618ee375c49fc86bc2beddf6dc17c3f17508d'
	'a8b49c56f4dfe371af6c3e1b214fb8274258823d702d334dbdec55e5a43671cd'
	'b941790009bb9eda46c2d96fb0d1c83b0fe2d1acf74754ab08e6b18c1e0c6b16'
	'193f7a6e0f422e24b1a3c1f02fa445c088e3c0665841a689511a95e923c1db29'
	'2720e418edaab7b305087b325604fe134dab1b743dae6e924aba7b4bd73ea389'
	'e8289ea4c1f8191e1e23661312ceee2128b8e790501b9a589d0d7bfc4384553f'
	'e525a83698e3363378f4582b43fd61c4002161acc84514188685e8b28b7cfbcf'
)

# Google API keys (see http://www.chromium.org/developers/how-tos/api-keys)
# Note: These are for Antergos Linux use ONLY. For your own distribution, please
# get your own set of keys. Feel free to contact info@antergos.com for
# more information.
_google_='AIzaSyBPgpho0q0siP2tggJwIsDx8ykpp-Y76Xk'

# Mozilla API keys (see https://location.services.mozilla.com/api)
# Note: These are for Antergos Linux use ONLY. For your own distribution, please
# get your own set of keys. Feel free to contact info@antergos.com for
# more information.
_mozilla_='fa35d4d6-6529-436f-b3a1-8b9af187eca2'


# BEGIN ANTBS METADATA
_autosums='True'
_is_monitored='False'
_monitored_service='http'
_monitored_type='firefox'
_monitored_version_url=''
# END ANTBS METADATA


prepare() {
	cd "${_pkgname}-${pkgver}"

	cp ../mozconfig .mozconfig
	
	# BEGIN Patches From Official Arch Package
	patch -Np1 -i ../firefox-install-dir.patch
	patch -Np1 -i ../fix-wifi-scanner.diff
	patch -Np1 -i ../no-crmf.diff
	# END Patches From Official Arch Package


	# BEGIN openSUSE's KDE Integration Patches
	patch -Np1 -i "${srcdir}/mozilla-nongnome-proxies.patch"
	patch -Np1 -i "${srcdir}/mozilla-kde.patch"
	patch -Np1 -i "${srcdir}/mozilla-language.patch"
	patch -Np1 -i "${srcdir}/firefox-kde.patch"
	patch -Np1 -i "${srcdir}/firefox-no-default-ualocale.patch"
	patch -Np1 -i "${srcdir}/firefox-branded-icons.patch"
	patch -Np1 -i "${srcdir}/mozilla-reduce-files-per-UnifiedBindings.patch"
	# END openSUSE's KDE Integration Patches

	echo -n "$_google_" >google-api-key
	echo "ac_add_options --with-google-api-keyfile=\"$PWD/google-api-key\"" >>.mozconfig

	echo -n "$_mozilla_" >mozilla-api-key
	echo "ac_add_options --with-mozilla-api-keyfile=\"$PWD/mozilla-api-key\"" >>.mozconfig

	[[ 'i686' = "${CARCH}" ]] && {
		_python="/32build/root${_python}"
		echo 'ac_add_options --target=i686-unknown-linux-gnu' >>.mozconfig
		echo 'ac_add_options --host=i686-unknown-linux-gnu' >>.mozconfig
	}

	_python=/usr/bin/python2

	mkdir "$srcdir/path"

	ln -s ${_python} "$srcdir/path/python"
}


build() {
	cd "${_pkgname}-${pkgver}" || cd "${_pkgname}-${_majorver}"

	# _FORTIFY_SOURCE causes configure failures
	CPPFLAGS+=" -O2"

	# Hardening
	LDFLAGS+=" -Wl,-z,now"

	# GCC 6
	CXXFLAGS+=" -fno-delete-null-pointer-checks -fno-schedule-insns2"

	export PATH="${srcdir}/path:$PATH"

	# Do PGO
	# xvfb-run -a -s "-extension GLX -screen 0 1280x1024x24" \
	# make -f client.mk build MOZ_PGO=1
	make -f client.mk build
}


package() {
	cd "${_pkgname}-${pkgver}"

	[[ "$CARCH" == "i686" ]] && cp "$srcdir/kde.js" obj-i686-*-linux-gnu/dist/bin/defaults/pref
	[[ "$CARCH" == "x86_64" ]] && cp "$srcdir/kde.js" obj-x86_64-*-linux-gnu/dist/bin/defaults/pref

	make -f client.mk DESTDIR="${pkgdir}" INSTALL_SDK= install

	install -Dm644 ../vendor.js "${pkgdir}/usr/lib/${_pkgname}/browser/defaults/preferences/vendor.js"
	install -Dm644 ../kde.js "${pkgdir}/usr/lib/firefox/browser/defaults/preferences/kde.js"

	for i in 16 22 24 32 48 256; do
		install -Dm644 "browser/branding/official/default${i}.png" \
			"${pkgdir}/usr/share/icons/hicolor/${i}x${i}/apps/${_pkgname}.png"
	done

	_branding='browser/branding/official'
	_hicolor="${pkgdir}/usr/share/icons/hicolor"
	install -Dm644 "${_branding}/content/icon64.png" "${_hicolor}/64x64/apps/${_pkgname}.png"
	install -Dm644 "${_branding}/mozicon128.png" "${_hicolor}/128x128/apps/${_pkgname}.png"
	install -Dm644 "${_branding}/content/about-logo.png" "${_hicolor}/192x192/apps/${_pkgname}.png"
	install -Dm644 "${_branding}/content/about-logo@2x.png" "${_hicolor}/384x384/apps/${_pkgname}.png"

	install -Dm644 ../firefox.desktop "${pkgdir}/usr/share/applications/firefox.desktop"

	# Use system-provided dictionaries
	rm -rf "${pkgdir}/usr/lib/${pkgname}"/{dictionaries,hyphenation}
	ln -s /usr/share/hunspell "${pkgdir}/usr/lib/${_pkgname}/dictionaries"
	ln -s /usr/share/hyphen "${pkgdir}/usr/lib/${_pkgname}/hyphenation"

	# Replace duplicate binary with symlink
	# https://bugzilla.mozilla.org/show_bug.cgi?id=658850
	ln -sf firefox "${pkgdir}/usr/lib/${_pkgname}/firefox-bin"
}


