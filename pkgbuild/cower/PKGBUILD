# Maintainer: Antergos Devs <devs@antergos.com>

pkgname=cower
pkgver=18
pkgrel=2
pkgdesc='A simple AUR agent with a pretentious name'
arch=('x86_64')
url='http://github.com/falconindy/cower'
license=('MIT')
depends=('curl' 'openssl' 'pacman' 'yajl')
makedepends=('perl')
source=("https://github.com/falconindy/$pkgname/archive/$pkgver.tar.gz")
md5sums=('94e537c61ce49602cd759b8dbf8226d6')

# BEGIN ANTBS METADATA
_is_monitored='True'
_monitored_service='github'
_monitored_type='tags'
_monitored_project='falconindy'
_monitored_repo='cower'
_monitored_match_pattern='/\d+/'
# END ANTBS METADATA

build() {
    cd "$pkgname-$pkgver"
    make
    sed '/^$/q' src/cower.c >LICENSE
}

package() {
    cd "$pkgname-$pkgver"

    make PREFIX=/usr DESTDIR="$pkgdir" install
    install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}



