# Maintainer: Antergos Developers <dev@antergos.com>
# Contributor: Guillaume Benoit <guillaume@manjaro.org>

pkgname=(pamac-dev pamac-tray-appindicator-dev)
pkgver=6.4.0
_pkgver=6.4.0
pkgrel=1
_gitcommit="master"
pkgdesc='A GTK3 frontend for libalpm'
arch=('x86_64')
url='https://github.com/manjaro/pamac'
license=('GPL3')
depends=(
	'glib2' 'json-glib' 'libsoup' 'dbus-glib' 'polkit' 'vte3' 'gtk3'
	'libnotify' 'desktop-file-utils' 'pacman' 'gnutls'
	'appstream-glib' 'archlinux-appstream-data'
)
makedepends=('gettext' 'itstool' 'vala' 'git' 'libappindicator-gtk3' 'meson')
backup=('etc/pamac.conf')
provides=('pamac')
conflicts=('pamac' 'pamac-aur')
source=("pamac-$pkgver::git+$url.git")
md5sums=('SKIP')


# BEGIN ANTBS METADATA
_is_monitored='True'
_monitored_service='github'
_monitored_type='commits'
_monitored_project='manjaro'
_monitored_repo='pamac'
# END ANTBS METADATA


_cd_srcdir() {
	{ [[ "${_gitcommit}" != "" ]] &&  cd "${srcdir}/pamac"; } || cd "${srcdir}/pamac-${_pkgver}"
}


pkgver() {
	cd "${srcdir}/pamac-${_pkgver}"

	_tag="$(git describe --abbrev=0 --tags $(git rev-list --tags --max-count=1))"

	printf "%s.git+%s" "${_tag,,/v}" "$(git rev-list --count HEAD)"
}


prepare() {
	_cd_srcdir

	# adjust version string
	sed -i -e "s|\"$_pkgver\"|\"$pkgver-$pkgrel\"|g" src/manager_window.vala 

	# patches here
}

build() {
	_cd_srcdir

	mkdir -p build && cd build

	meson --prefix=/usr --sysconfdir=/etc --libdir=lib -Denable-appindicator=true ..
	ninja
}

package_pamac-dev() {
	optdepends=(
		'pamac-tray-appindicator-dev: tray icon for KDE'
	)
	conflicts=('pamac')
	provides=('pamac')
	backup=('etc/pamac.conf')


	_cd_srcdir
	cd build
	DESTDIR="${pkgdir}" ninja install

	# Enable applicable systemd timers
	mkdir -p "${pkgdir}/etc/systemd/system/multi-user.target.wants"
	ln -sf /usr/lib/systemd/system/pamac-cleancache.timer "${pkgdir}/etc/systemd/system/multi-user.target.wants"

	# Remove pamac-tray-appindicator
	rm "${pkgdir}/usr/bin/pamac-tray-appindicator"
	rm -f "${pkgdir}/etc/xdg/autostart/pamac-tray-appindicator.desktop"
}

package_pamac-tray-appindicator-dev() {
	pkgdesc='Tray icon using appindicator for improved KDE support.'
	depends=('pamac-dev' 'libappindicator-gtk3')
	conflicts=('pamac-tray-appindicator')
	provides=('pamac-tray-appindicator')
	backup=()

	_cd_srcdir
	cd build
	install -Dm755 src/pamac-tray-appindicator "${pkgdir}/usr/bin/pamac-tray-appindicator"
	install -Dm644 ../data/applications/pamac-tray-appindicator.desktop "${pkgdir}/etc/xdg/autostart/pamac-tray-appindicator.desktop"
}


