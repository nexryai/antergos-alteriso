# Maintainer: Antergos Developers <dev@antergos.com>
# Contributor: Doug Newgard <scimmia at archlinux dot info>

pkgname=rage
pkgver=0.2.1
pkgrel=1
pkgdesc='Video Player based on EFL'
arch=('x86_64')
url="https://www.enlightenment.org/p.php?p=about/rage"
license=('BSD')
depends=('efl')
source=("http://download.enlightenment.org/rel/apps/$pkgname/$pkgname-$pkgver.tar.xz")
md5sums=('f656e037bf0724f1539dd37e90000da3')

build() {
	cd $pkgname-$pkgver
	export CFLAGS="$CFLAGS -fvisibility=hidden"
	./configure --prefix=/usr
	make
}

package() {
	cd $pkgname-$pkgver

	make -j1 DESTDIR="$pkgdir" install

	# Install text files
	install -d "$pkgdir/usr/share/doc/$pkgname/"
	install -m644 -t "$pkgdir/usr/share/doc/$pkgname/" ChangeLog NEWS README

	# Install license files
	install -d "$pkgdir/usr/share/licenses/$pkgname/"
	install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname/" AUTHORS COPYING
}


