# Maintainer: Antergos Developers <dev@antergos.com>

pkgname=antergos-alerts
pkgver=18.9.9
pkgrel=1
pkgdesc='Notifies users when new antergos alerts are issued.'
arch=('any')
depends=('pacman' 'python-termcolor' 'libnotify' 'gettext')
replaces=('antergos-repo-priority')
conflicts=('antergos-repo-priority')
groups=('antergos-base')
url="https://antergos.com/"
license=('GPL3')
source=("${pkgname}-${pkgver}.zip::https://github.com/Antergos/${pkgname}/archive/${pkgver}.zip")
md5sums=('cabcfcc20522a1233bcaef558b868531')

# BEGIN ANTBS METADATA
_is_monitored='True'
_monitored_service='github'
_monitored_type='releases'
_monitored_project='antergos'
_monitored_repo='antergos-alerts'
# END ANTBS METADATA

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install

	for fpath in po/*; do
		if [[ -f "${fpath}" ]] && [[ "${fpath}" != 'po/ANTERGOS_NOTIFY.pot' ]]; then
			STRING_PO=`echo ${fpath#*/}`
			STRING=`echo ${STRING_PO%.po}`
			mkdir -p "${pkgdir}/usr/share/locale/${STRING}/LC_MESSAGES"
			msgfmt "${fpath}" -o "${pkgdir}/usr/share/locale/${STRING}/LC_MESSAGES/ANTERGOS_NOTIFY.mo"
			echo "${STRING} installed..."
		fi
	done
}


